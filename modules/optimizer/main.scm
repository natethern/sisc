(define (opt:optimize e)
  (let-values ([(rv state) (opt e (scan e))])
    rv))

(define opt
  (lambda (e state)
    (match e
      (,x
       (guard (symbol? x))
       (opt:ref x state))
      ((quote ,x) (values `(quote ,x) (new-state)))
      ((begin ,[exp1 exp1-state] ,[exps* exps-state*] ...)
       (let-values ([(rv state) (opt:begin exp1 exps* state)])
         (values rv (merge-states state exp1-state 
                                  (apply merge-states exps-state*)))))
      ((if ,[test test-state] ,[conseq conseq-state] ,[altern altern-state])
       (let-values ([(rv state) (opt:if test conseq altern state)])
         (values rv (merge-states state test-state conseq-state altern-state))))
      ;; Lets
      (((lambda ,formals ,body) ,[values* values-state*] ...)
       (guard (list? formals))
       (let-values ([(rv state) (opt:let formals values* body state)])
         (values rv (merge-states state (apply merge-states values-state*)))))
      ((lambda ,formals ,body)
       (opt:lambda formals body state))
      ((letrec ((,lhs* ,[rhs* rhs-state*]) ...) ,body)
       (let-values ([(rv state) (opt:letrec lhs* rhs* body state)])
         (values rv (merge-states state (apply merge-states rhs-state*)))))
      ((define ,lhs ,[rhs rhs-state])
       (values `(define ,lhs ,rhs) rhs-state))
      ((set! ,formal ,[value vstate])
       (let-values ([(rv state) (opt:set! formal value state)])
         (values rv (merge-states state vstate))))     
      ((compile-in-annotation ,[expr expr-state] ,annotation)
       (values `(compile-in-annotation ,expr ,annotation)
               expr-state))
      ((,[rator rator-state] ,[rands* rands-state*] ...)
       (let-values ([(rv state) (opt:application rator rands* state)])
         (values rv (merge-states state rator-state 
                                  (apply merge-states rands-state*)))))
      (,other (error 'optimizer "Unrecognized s-expression: ~a" other)))))

(define (scan expr)
  (match expr
    (,x 
     (guard (symbol? x))
     `((refed-vars ,x)))
    ((quote ,x) (new-state))
    ((begin ,[exp1] ,[exps*] ...)
     (merge-states exp1 (apply merge-states exps*)))
    ((if ,[test] ,[conseq] ,[altern])
     (merge-states test conseq altern))
    (((lambda ,formals ,[body]) ,[values*] ...)
     (merge-states body (apply merge-states values*)))
    ((lambda ,formals ,[body]) body)
    ((letrec ((,lhs* ,[rhs*]) ...) ,[body])
     (merge-states (apply merge-states rhs*) body))
    ((define ,lhs ,[rhs]) rhs)
    ((set! ,formal ,[value]) 
     (union-state-entry
      (union-state-entry value 'set-vars formal)
      'refed-vars formal))
    ((compile-in-annotation ,[expr] ,annotation) expr)
    ((,[rator] ,[rands*] ...)
     (merge-states rator (apply merge-states rands*)))
    (,other (error 'optimizer "Unrecognized s-expression: ~a" other))))

